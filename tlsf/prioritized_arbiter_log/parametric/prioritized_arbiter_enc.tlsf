INFO {
  TITLE:       "Prioritized Arbiter (binary encoded version)"
  DESCRIPTION: "This variant of the prioritized arbiter avoids the linear rise
in the number of inputs with increasing number of clients. To this
end, a client request is encoded in binary using only logarithmically
many bits. This way only one request can be made per time step, which
is answerd by a respective binary encoded grant the same way. As a
consequence, without further restrictions, every request could be
answered immediately. Hence, to complicate the situation again, the
output player is forced to repeate the same grant for one time step
once in a while. This way, it can be possible for the input player to
open multiple requests faster than they can be answered."
  SEMANTICS:   Mealy
  TARGET:      Mealy
}

GLOBAL {
  PARAMETERS {
    n = 8;    // nâ‰¥1
  }

  DEFINITIONS {
    /* Checks whether a bus of size log(n) currently represents
     * the numerical value v (encoded in binary).
     */
    value(bus,v) = value'(bus,v,0,SIZEOF bus);
    value'(bus,v,i,j) =
      i >= j        : true
      bit(v,i) == 1 : value'(bus,v,i + 1,j) &&  bus[i]
      otherwise     : value'(bus,v,i + 1,j) && !bus[i];

    /* Returns the i-th bit of the numerical value v. */
    bit(v,i) =
      i <= 0     : v % 2
      otherwise  : bit(v / 2,i - 1);

    /* floor(log2) */
    log2_dn(x) =
      x <= 1    : 0
      otherwise : 1 + log2_dn(x/2);

    /* nbits(x): return the number of bits needed to encode x values
     * This is similar to ceil(log2) except for value 1: nbits(1) = 1 while ceil(log2(1)) = 0
    */
    nbits(x) =
      x == 0    : 0
      otherwise : 1 + log2_dn(x-1);
  }
}

MAIN {
  INPUTS {
    // using the encoded value of 0 as a request of the master grant
    request[nbits(n)];
  }

  OUTPUTS {
    // allow one more value in the outputs indicating no selected grant
    grant[nbits(n+1)];
  }

  ASSUMPTIONS {
    // The request master cannot starve the others (i.e., we require inf-oft a nonzero request)
    G F (||[0 <= i < nbits(n)] request[i]);
  }

  INVARIANTS {
    // every request (except master) is eventually granted
    &&[0 < i < n]
      (value(request, i) -> F value(grant, i));

    // a master request is granted before any other request
    G (value(request, 0) -> X (&&[0 < i < n] !value(grant, i) U value(grant, 0)));
  }

  GUARANTEE {
    // sometimes there has to be a delay to allow requests to be
    // produced faster that they can be granted.
    G F (&&[0 <= i < nbits(n)] (grant[i] <-> X grant[i]));
  }
}
