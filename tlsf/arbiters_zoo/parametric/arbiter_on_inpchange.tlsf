INFO
{
  TITLE:       ""
  DESCRIPTION: "
                Arbiter that reacts to a change of input signal (from 0 to 1 or from 1 to 0). At least that was the intention of the spec.
               "
  SEMANTICS:   Mealy
  TARGET:      Mealy
}

GLOBAL
{
  PARAMETERS {
  	n = 1; // nâ‰¥1
  }

  DEFINITIONS
  {
    // copied from simple arbiter
    mutual_exclusion(bus) = mone(bus,0,(SIZEOF bus) - 1);
    none(bus,i,j) = &&[i <= t <= j] !bus[t];
    mone(bus,i,j) =
    i > j : false
    i == j : true
    i < j :
      (none(bus, i, m(i,j)) && mone(bus, m(i,j) + 1, j)) ||
      (mone(bus, i, m(i,j)) && none(bus, m(i,j) + 1, j));
    m(i,j) = (i + j) / 2;

    //

    lower_grant_eventually(r,g) =
      G(g && (G !r) -> F !g);

    no_spurious_grants(r,g) =
    (
      (!g W r) &&                         // initially
      G(g && X(!r && !g) -> X(!g W r))    // always
    );

    every_req_is_granted(r,g) = G(r -> F g);

    signal_change(s) = (s && X !s) || ((!s) && X s);
  }
}

MAIN
{
  INPUTS  { s[n]; }
  OUTPUTS { g[n]; }

  GUARANTEES
  {
    G mutual_exclusion(g);

    &&[0 <= i < n]
    (
      no_spurious_grants(signal_change(s[i]), g[i]) &&
      lower_grant_eventually(signal_change(s[i]), g[i]) &&
      every_req_is_granted(signal_change(s[i]), g[i])
    );
  }
}
